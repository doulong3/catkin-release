# Remove duplicate libraries, generalized from PCLConfig.cmake.in
macro(remove_duplicate_libraries _unfiltered_libraries _final_filtered_libraries)
  set(_filtered_libraries)
  set(_debug_libraries)
  set(_optimized_libraries)
  set(_other_libraries)
  set(_waiting_for_debug 0)
  set(_waiting_for_optimized 0)
  set(_library_position -1)
  foreach(library ${${_unfiltered_libraries}})
    if("${library}" STREQUAL "debug")
      set(_waiting_for_debug 1)
    elseif("${library}" STREQUAL "optimized")
      set(_waiting_for_optimized 1)
    elseif(_waiting_for_debug)
      list(FIND _debug_libraries "${library}" library_position)
      if(library_position EQUAL -1)
        list(APPEND ${_filtered_libraries} debug ${library})
        list(APPEND _debug_libraries ${library})
      endif()
      set(_waiting_for_debug 0)
    elseif(_waiting_for_optimized)
      list(FIND _optimized_libraries "${library}" library_position)
      if(library_position EQUAL -1)
        list(APPEND ${_filtered_libraries} optimized ${library})
        list(APPEND _optimized_libraries ${library})
      endif()
      set(_waiting_for_optimized 0)
    else("${library}" STREQUAL "debug")
      list(FIND _other_libraries "${library}" library_position)
      if(library_position EQUAL -1)
        list(APPEND ${_filtered_libraries} ${library})
        list(APPEND _other_libraries ${library})
      endif()
    endif("${library}" STREQUAL "debug")
  endforeach(library)
  set(_final_filtered_libraries _filtered_libraries)
endmacro(remove_duplicate_libraries)


if (@PACKAGE_NAME@_CONFIG_INCLUDED)
   return()
endif()
set(@PACKAGE_NAME@_CONFIG_INCLUDED TRUE)

if (NOT "X@PACKAGE_INCLUDE_DIRS@" STREQUAL "X")
  set(@PACKAGE_NAME@_INCLUDE_DIRS_ )
  foreach(idir @PACKAGE_INCLUDE_DIRS@)
    if(IS_ABSOLUTE ${idir} AND IS_DIRECTORY ${idir})
      set(include ${idir})
    elseif(IS_DIRECTORY @PKG_INCLUDE_PREFIX@/${idir})
      set(include @PKG_INCLUDE_PREFIX@/${idir})
    else()
      message(FATAL_ERROR "@PACKAGE_NAME@ specified ${idir} as an include dir, but I can't find it.
It does not exist, as an absolute directory or in @PKG_INCLUDE_PREFIX@/${idir}
Tell @Maintainer@ to fix it."
             )
    endif()
    list(APPEND @PACKAGE_NAME@_INCLUDE_DIRS_  ${include})
  endforeach()
  set(@PACKAGE_NAME@_INCLUDE_DIRS ${@PACKAGE_NAME@_INCLUDE_DIRS_}
  CACHE FILEPATH "Includes for @PACKAGE_NAME@")
endif()

foreach(lib @PACKAGE_LIBRARIES@)
  if (NOT TARGET ${lib})
    message(FATAL_ERROR "In project ${PROJECT_NAME} I'm looking for ${lib} via find_package.  The source for @PACKAGE_NAME@ is present in workspace, but ${lib} is not a target.  Did you find_package() this thing before the subdirectory containing its code is included?")
  endif()
  list(APPEND @PACKAGE_NAME@_LIBRARIES ${lib})
endforeach()

foreach(dep @PACKAGE_DEPENDS@)
  if(NOT ${dep}_FOUND)
    find_package(${dep})
  endif()
  list(APPEND @PACKAGE_NAME@_INCLUDE_DIRS ${${dep}_INCLUDE_DIRS})
  list(APPEND @PACKAGE_NAME@_LIBRARIES ${${dep}_LIBRARIES})
endforeach()

if (@PACKAGE_NAME@_INCLUDE_DIRS)
  list(REMOVE_DUPLICATES @PACKAGE_NAME@_INCLUDE_DIRS)
endif()
if(@PACKAGE_NAME@_LIBRARIES)
  remove_duplicate_libraries(@PACKAGE_NAME@_LIBRARIES @PACKAGE_NAME@_LIBRARIES)
endif()

foreach(extra @PACKAGE_CFG_EXTRAS@)
  include(@PKG_CMAKE_DIR@/${extra})
endforeach()
